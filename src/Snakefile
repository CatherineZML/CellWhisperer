"""
This root Snakefile downloads the models and all evaluation datasets and then runs all analyses leading to the final figure plots.
"""

from snakemake.remote.HTTP import RemoteProvider as HTTPRemoteProvider
from pathlib import Path
import subprocess

PROJECT_DIR = Path(subprocess.check_output("git rev-parse --show-toplevel", shell=True).decode("utf-8").strip())
configfile: PROJECT_DIR / "config.yaml"

HTTP = HTTPRemoteProvider()

module datasets:
    snakefile: "datasets/Snakefile"
    config: config

module figures:
    snakefile: "figures/Snakefile"
    config: config

use rule * from datasets as datasets_*
use rule * from figures as figures_*

GSVA_COMPUTED_DATASETS = ["human_disease", "archs4_geo", "tabula_sapiens", "cellxgene_census"]
rule download_gsva_results:
    """
    Downloading GSVA results, since they are extremely slow to compute

    If you want to run the whole GSVA pipeline, simply comment out this rule and include gsva:
    `include: "../../shared/rules/gsva.smk"`
    """
    input:
        [HTTP.remote(f"{config['precomputing_base_url']}/datasets/{dataset}/gsva.parquet", keep_local=False)[0] for dataset in GSVA_COMPUTED_DATASETS]
    output:
        gsva_results=expand(PROJECT_DIR / config["paths"]["gsva"]["result"], dataset=GSVA_COMPUTED_DATASETS),
    run:
        import shutil
        for fin, fout in zip(input, output):
            shutil.copy(fin, fout)


# include: "shared/rules/gsva.smk"  # Alternatively compute them instead of downloading


LLAVA_BASE_MODEL = config["model_name_path_map"]["llava_base_llm"]
GPU_TYPE = "a100-sxm4-80gb"

include: "cellxgene_preprocessing/rules/prepare_cellxgene_dataset.smk"
include: "shared/rules/download_models.smk"

rule all:
    input:
        rules.datasets_all.input,
        rules.figures_all.input,
        rules.download_cellwhisperer_embedding_model.output,  # Download included through
        rules.download_cellwhisperer_llm.output,

rule models:
    input:
        rules.download_cellwhisperer_embedding_model.output,  # Download included through
        rules.download_cellwhisperer_llm.output,
        rules.download_mixtral.output,
        rules.download_geneformer.output,
